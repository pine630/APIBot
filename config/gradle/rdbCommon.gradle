// RDB 関連設定

project.ext.useSpringBoot = true

if (useSpringBoot) {
    println "$name <- RDB (Common) with SpringBoot"
    dependencies {
        // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-jdbc
        compile(group: "org.springframework.boot", name: "spring-boot-starter-jdbc")

        // https://mvnrepository.com/artifact/org.seasar.doma.boot/doma-spring-boot-starter
        compile(group: "org.seasar.doma.boot", name: "doma-spring-boot-starter")
    }
} else {
    println "$name <- RDB (Common) without SpringBoot"
}

// Domaの使用を他のタスクに伝達
project.ext.useDoma = true

// Doma-Genで出力するJavaクラスとSQLファイルの出力先ディレクトリを同じにする
processResources.destinationDir = compileJava.destinationDir
processTestResources.destinationDir = compileTestJava.destinationDir

// Doma-Genでコンパイルより前にSQLファイルを出力先ディレクトリにコピーする
// ために依存関係を逆転する
compileJava.dependsOn processResources
compileTestJava.dependsOn processTestResources

// Doma-Gen用の追加定義
configurations {
    domaGenRuntime
}

// 依存関係設定
dependencies {
    compile(group: "org.seasar.doma", name: "doma")

    // https://mvnrepository.com/artifact/org.bgee.log4jdbc-log4j2/log4jdbc-log4j2-jdbc4.1
    compile(group: "org.bgee.log4jdbc-log4j2", name: "log4jdbc-log4j2-jdbc4.1")

    // Doma-Gen本体
    domaGenRuntime("org.seasar.doma:doma-gen:${domaVersion}")

    // Doma-Gen接続用設定
    domaGenRuntime(domaGenJdbc)
}
